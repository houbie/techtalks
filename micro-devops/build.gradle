buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        //asciidoctor dependencies
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.1.5'
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta1'
        classpath 'commons-httpclient:commons-httpclient:3.1'
        classpath 'com.bluepapa32:gradle-watch-plugin:0.1.5'
    }
}

apply plugin: 'groovy'

//asciidoctor plugins
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'

def mainVersion = file('version').text.trim()
def buildNumber = System.env.BUILD_NUMBER
if (buildNumber) {
    ext.projectVersion = "$mainVersion.$buildNumber"
    file('build-version').text = ext.projectVersion
} else {
    ext.projectVersion = "$mainVersion-SNAPSHOT"
}
println "project version: $projectVersion"

group = 'be.ixor.techtalks'
version = projectVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8


jar {
    manifest {
        attributes 'Implementation-Title': 'Micro-devOps-sample-lib',
                'Implementation-Version': version,
                'Main-Class': 'be.ixor.techtalks.devops.VersionInfo'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.9'

    //asciidoctor dependencies
    gems 'rubygems:slim:2.1.0'
    gems 'rubygems:thread_safe:0.3.4'

}

ext {
    revealjsVersion = 'master'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir, 'download')
    templateDir = new File(downloadDir, 'templates')
    revealjsDir = new File(downloadDir, 'reveal.js')
}

task download {
    description "Download extra revealjs resources"
    outputs.dir templateDir
    outputs.dir revealjsDir
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                    templateDir, recursive: true, overwrite: true
            cp "zip:https://github.com/houbie/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                    revealjsDir, recursive: true, overwrite: true
        }
    }
}

//everything that follows is only asciidocotor related
asciidoctor {
    sources {
        include '*.adoc'
    }

    resources {
        from(sourceDir) {
            include 'images/**'
        }
        from(downloadDir) {
            include 'reveal.js/**'
        }
    }

    backends 'revealjs'

    attributes 'sourcedir': file('src/javascript'),
            'source-highlighter': 'highlightjs',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': 'true',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': 'true',
            'revealjs_theme': 'ixor',
            'revealjs_transition': 'convex',
            'revealjs_history': 'true',
            'revealjs_slideNumber': 'true'

    options template_dirs: [new File(templateDir, 'templates/slim').absolutePath]
}
