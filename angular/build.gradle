buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.1.5'
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta1'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'

version = '1.0.0-SNAPSHOT'
defaultTasks 'asciidoctor'

ext {
    revealjsVersion = 'master'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir, 'download')
    templateDir = new File(downloadDir, 'templates')
    revealjsDir = new File(downloadDir, 'reveal.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:slim:2.1.0'
    gems 'rubygems:thread_safe:0.3.4'
}

defaultTasks 'jrubyPrepareGems', 'download', 'asciidoctor'

task download {
    description "Download extra revealjs resources"
    outputs.dir templateDir
    outputs.dir revealjsDir
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                    templateDir, recursive: true, overwrite: true
            cp "zip:https://github.com/houbie/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                    revealjsDir, recursive: true, overwrite: true
        }
    }
}

asciidoctor {
    sources {
        include 'angular-slides.adoc'
    }

    resources {
        from(sourceDir) {
            include 'images/**'
        }
        from(downloadDir) {
            include 'reveal.js/**'
        }
    }

    backends 'revealjs'

    attributes 'sourcedir': file('src/javascript'),
            'source-highlighter': 'highlightjs',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': 'true',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': 'true',
            'revealjs_theme': 'ixor',
            'revealjs_transition': 'convex',
            'revealjs_history': 'true',
            'revealjs_slideNumber': 'true'

    options template_dirs: [new File(templateDir, 'templates/slim').absolutePath]
}

//asciidoctor.dependsOn download
build.dependsOn asciidoctor